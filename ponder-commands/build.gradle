plugins {
    id 'java-library'
    id 'org.jetbrains.kotlin.jvm' version '1.7.0'
    id 'maven-publish'
}

group = 'com.dansplugins'
version = "2.0.0"

def repoUsername = ""
def repoPassword = ""
def propertiesFile = new File("repo.properties")
if (propertiesFile.exists()) {
    def properties = new Properties()
    properties.load(new FileInputStream(propertiesFile))
    repoUsername = properties.getProperty("repo-username")
    repoPassword = properties.getProperty("repo-password")
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core-jvm:1.6.3'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.8.1'
    testImplementation 'io.mockk:mockk:1.12.4'
}

tasks.named('test') {
    useJUnitPlatform()
}

publishing {
    repositories {
        maven {
            name = "DansPlugins"
            credentials {
                username repoUsername
                password repoPassword
            }
            url "https://repo.dansplugins.com/repository/maven-releases/"
        }
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/Dans-Plugins/Ponder")
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
    publications {
        maven(MavenPublication) {
            groupId = project(":ponder-commands").group
            artifactId = project(":ponder-commands").name
            version = project(":ponder-commands").version

            from components.java
        }
    }
}